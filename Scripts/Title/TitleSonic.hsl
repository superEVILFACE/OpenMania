event TitleSonic_Load() {
    print "TitleSonic_Load()";
}

class TitleSonic {
    Create() {
        this.Sprite = Resources.LoadSprite("Sprites/Title/Sonic.bin", SCOPE_SCENE);
        this.Priority = 4;

        this.DoUpdate = false;
        this.ViewRenderFlag = 0;
        this.AutoAnimate = false;

        this.InitialY = this.Y;
        this.SetAnimation(0, 0);

        DebugMode_AddObject("TitleSonic", this.DebugCreate, this.DebugRender);
    }

    Update() {
        if (!this.DoUpdate)
            return;

        if (this.CurrentAnimation == 0 &&
            this.CurrentFrame == 47) {
            this.SetAnimation(1, 0);
        }

        this.Animate();
    }

    Render() {
        var vw = View.GetWidth(0);

        Draw.SetClip(0.0, 0.0, vw, 160.0);
        if (this.CurrentAnimation == 1)
            Draw.Sprite(this.Sprite, 0, 48, this.X, this.Y, false, false);

        Draw.Sprite(this.Sprite, this.CurrentAnimation, this.CurrentFrame, this.X, this.Y, false, false);
        Draw.ClearClip();
    }

    event DebugCreate()
    {
        Instance.Create("TitleSonic", DebugMode.X, DebugMode.Y);
    }

    event DebugRender()
    {
        Draw.Sprite(Resources.LoadSprite("Sprites/Title/Sonic.bin", SCOPE_SCENE), 0, 0, DebugMode.Player.X, DebugMode.Player.Y, false, false);
    }
}
